buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "com.bmuschko:gradle-docker-plugin:$gradleDockerPluginVersion"
    }
}

plugins {
	id "net.researchgate.release" version "2.0.2"
	id "com.jfrog.bintray" version "1.0"
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "spring-boot"
apply plugin: "com.bmuschko.docker-java-application"
apply plugin: "maven-publish"

sourceCompatibility = 1.8

group = "org.rocklass"

jar {
	baseName = jarBaseName
	doFirst {
	    manifest {
	        attributes 	"Implementation-Title": title,
	        			"Implementation-Version": version
	    }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
	baseName = jarBaseName
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
    testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
}

// filter resources with properties
import org.apache.tools.ant.filters.ReplaceTokens

processResources {
	def properties = new Properties()
    file("gradle.properties").withInputStream {
        properties.load(it);   
    }
    filter(ReplaceTokens, tokens: properties)
}

docker {
    url = dockerUrl // from GRADLE_USER_HOME/gradle.properties
    certPath = new File(dockerCertPath) // from GRADLE_USER_HOME/gradle.properties

    javaApplication {
        baseImage = "dockerfile/java:oracle-java8"

        maintainer = "rocklass"
        tag = "$maintainer/$jarBaseName:$version"
        port = serverPort as Integer
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            
            artifact sourcesJar {
                classifier "sources"
            }
        }
    }
}

// update version during release (no snapshot)
confirmReleaseVersion << {
    publishing.publications.mavenJava.version = project.version
    jar.version = project.version
    sourcesJar.version = project.version
    publishing.publications.mavenJava.setArtifacts([jar, sourcesJar])
    bintrayUpload.versionName = project.version
    bintrayUpload.versionVcsTag = project.version
}

uploadArchives {
    repositories {
       flatDir {
           dirs "repository"
       }
    }
}

release {
	failOnPublishNeeded = false
	tagPrefix = project.name
}

// upload to bintray during release
createReleaseTag.dependsOn bintrayUpload

bintray {
    user = bintrayUser // from GRADLE_USER_HOME/gradle.properties
    key = bintrayKey // from GRADLE_USER_HOME/gradle.properties

    publications = ["mavenJava"]
    dryRun = false
    publish = true
    pkg {
        repo = "maven"
        name = project.name
        desc = "Full Stack Laboratory"
        websiteUrl = "https://github.com/rocklass/fullstacklab"
        issueTrackerUrl = "https://github.com/rocklass/fullstacklab/issues"
        vcsUrl = "https://github.com/rocklass/fullstacklab.git"
        licenses = ["GPL-3.0"]
        publicDownloadNumbers = true
        version {
                name = project.version
                vcsTag = project.version
        }
    }
}